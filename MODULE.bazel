# *******************************************************************************
# Copyright (c) 2024-2025 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache License Version 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0
#
# SPDX-License-Identifier: Apache-2.0
# *******************************************************************************
module(name = "communication")


bazel_dep(name = "platforms", version = "0.0.11")
bazel_dep(name = "score_toolchains_gcc", version = "0.4", dev_dependency=True)

gcc = use_extension("@score_toolchains_gcc//extentions:gcc.bzl", "gcc", dev_dependency=True)
gcc.toolchain(
    url = "https://github.com/eclipse-score/toolchains_gcc_packages/releases/download/0.0.1/x86_64-unknown-linux-gnu_gcc12.tar.gz",
    sha256 = "457f5f20f57528033cb840d708b507050d711ae93e009388847e113b11bf3600",
    strip_prefix = "x86_64-unknown-linux-gnu",
)

# TODO to be moved to toolchain. https://github.com/eclipse-score/toolchains_gcc/issues/11
gcc.extra_features(
    features = [
        "minimal_warnings",
        "treat_warnings_as_errors",
    ],
)
gcc.warning_flags(
    minimal_warnings = ["-Wall", "-Wno-error=deprecated-declarations"],
    strict_warnings = ["-Wextra", "-Wpedantic"],
    treat_warnings_as_errors = ["-Werror"],
)

use_repo(gcc, "gcc_toolchain", "gcc_toolchain_gcc")

bazel_dep(name = "googletest", version = "1.15.0")
bazel_dep(name = "google_benchmark", version = "1.9.1")

bazel_dep(name = "rules_rust", version = "0.61.0")
bazel_dep(name = "score_crates", version = "0.0.2", repo_name = "crate_index")

bazel_dep(name = "rules_boost", repo_name = "com_github_nelhage_rules_boost")
archive_override(
    module_name = "rules_boost",
    urls = ["https://github.com/nelhage/rules_boost/archive/refs/heads/master.tar.gz"],
    strip_prefix = "rules_boost-master",
)
bazel_dep(name = "boost.program_options", version = "1.87.0")


bazel_dep(name = "download_utils", version = "1.0.1")
download_archive = use_repo_rule("@download_utils//download/archive:defs.bzl", "download_archive")
download_archive(
    name = "json_schema_validator",
    urls = ["https://github.com/pboettch/json-schema-validator/archive/refs/tags/2.1.0.tar.gz"],
    build = "//third_party/json_schema_validator:json_schema_validator.BUILD",
    strip_prefix = "json-schema-validator-2.1.0",
)

download_archive(
    name = "jsonschema",
    urls = ["https://github.com/python-jsonschema/jsonschema/archive/refs/tags/v4.23.0.tar.gz"],
    build = "//third_party/jsonschema:jsonschema.BUILD",
    strip_prefix = "jsonschema-4.23.0",
)

bazel_dep(name = "nlohmann_json", version = "3.11.3")
bazel_dep(name = "bazel_skylib", version = "1.7.1")

bazel_dep(name = "score-baselibs", version = "0.0.0")
git_override(
    module_name = "score-baselibs",
    remote = "https://github.com/eclipse-score/baselibs.git",
    commit = "8f041f9cc1fa596585f8a8bb71bd90c06252e017",
)

# Python 3.12 toolchain for Bazel (required for LOBSTER/TRLC dependencies)
bazel_dep(name = "rules_python", version = "0.32.0")
python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.12",
)
 
# Python pip dependencies for traceability tools
pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "communication_pip",
    python_version = "3.12",
    requirements_lock = "//third_party/python:requirements.txt",
)
use_repo(pip, "communication_pip")

# TRLC dependency for requirements traceability
bazel_dep(name = "trlc", version = "0.0.0")
git_override(
    module_name = "trlc",
    remote = "https://github.com/bmw-software-engineering/trlc.git",
    commit = "650b51a47264a4f232b3341f473527710fc32669",  # trlc-2.0.2 release
)

lobster_ext = use_extension("//third_party/lobster:lobster.bzl", "lobster_ext")
use_repo(lobster_ext, "lobster")
# Use lobster's upstream requirements.txt directly for this module's Python hub
pip.parse(
    hub_name = "lobster_dependencies",
    python_version = "3.12",
    requirements_lock = "@lobster//:requirements.txt",
)
use_repo(pip, "lobster_dependencies")
